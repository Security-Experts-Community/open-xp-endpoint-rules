JSON = ''
COND = (
    $eventid != null and
    $node != null and
    $timestamp != null and
    $items["GRP_MGMT"] != null
)

$kv_main = keyvalue($items["GRP_MGMT"][0], " ", "=", "'")
$kv_msg = keyvalue($kv_main["msg"], " ", "=", '"')

if $kv_main["auid"] != "4294967295" then
    subject = "account"
endif

action = "modify"
object = "user_group"

if $kv_msg["exe"] != "?" then
    $exe = strip($kv_msg["exe"], '"', '"')
    if $exe == $kv_msg["exe"] then
        $exe = hex_to_string($kv_msg["exe"])
    endif
    
    $process_path_and_name = csv($exe, "/", "")
    $process_fullpath = $exe
    $process_name = $process_path_and_name[length($process_path_and_name) - 1]
    $process_path = strip($exe, "", $process_name)
endif

if ($kv_msg["res"] == "success") and ($process_name == "groupmod") and find_substr($kv_msg["op"], "changing-") != null then
    status = "ongoing"
elif $kv_msg["res"] == "success"  then
    status = "success"
else
    status = "failure"
endif

$auid_list = csv(strip($kv_main["AUID"], '"', '"'), "@", "")
$uid_list = csv(strip($kv_main["UID"], '"', '"'), "@", "")

if $kv_main["auid"] != "4294967295" then
    subject.account.name = $auid_list[0]
    subject.account.domain = $auid_list[1]

    subject.account.id = $kv_main["auid"]
else
    subject.account.name = $uid_list[0]
    subject.account.domain = $uid_list[1]

    subject.account.id = $kv_main["uid"]
endif

subject.account.session_id = $kv_main["ses"]
subject.account.privileges = $kv_main["uid"]

subject.process.fullpath = $process_fullpath
subject.process.name = $process_name
subject.process.path = $process_path
subject.process.id = $kv_main["pid"]

object.state = $kv_msg["op"]

$property = ""
$value = ""
$new_value = ""

if ($process_name == "groupmod") and (find_substr($kv_msg["op"], "changing") != null) then
    if $kv_msg["new_group"] != null then
        $property = $property + " | name"
        $value = $value + " | " + $kv_msg["grp"]
        $new_value = $new_value + " | " + $kv_msg["new_group"]
    endif
    
    if $kv_msg["new_gid"] != null then
        $property = $property + " | id"
        $value = $value + " | " + $kv_msg["gid"]
        $new_value = $new_value + " | " + $kv_msg["new_gid"]
    endif

    object.name = $old_name
    object.id = $old_gid

    object.property = join($key_list, '|')
    object.value = join($list, '|')
endif

object.name = $kv_msg["grp"]
object.id = $kv_msg["gid"]

object.property = if $property != "" then strip($property, " | ", "") else null endif
object.value = if $value != "" then strip($value, " | ", "") else null endif
object.new_value = if $new_value != "" then strip($new_value, " | ", "") else null endif

if $kv_msg["terminal"] != "?" then
    logon_service = replace(strip($kv_msg["terminal"], "/dev/", ""), "", "/")
endif

chain_id = $eventid  # AuditD Event ID

reason = $kv_msg["op"]

time = epoch_to_datetime(number($timestamp))
msgid = "GRP_MGMT"
importance = "low"

category.generic = "Group"
category.high = "Users And Rights Management"
category.low = "Manipulation"

event_src.title = "unix_like"
event_src.subsys = "auditd"
event_src.category = "Operating system"

if $node != "" then
    if maybe_ipv4($node) != null or maybe_ipv6($node) != null then
        if (in_subnet($node, "127.0.0.0/8") == False and $node != "::1") then
            event_src.ip = $node
            labels = "node_contains_ext_ip"
        endif
    else
        $first_dot = find_substr($node, ".")
        if $first_dot != null then
            event_src.fqdn = $node
            event_src.hostname = substr($node, 0, $first_dot)
        else
            event_src.hostname = $node
        endif
    endif
else
    event_src.ip = recv_ipv4
endif

if (event_src.hostname == null) and not in_list([$kv_msg["addr"], "?"], $kv_msg["hostname"]) then
    event_src.hostname = $kv_msg["hostname"]
endif

if (event_src.ip == null) and ($kv_msg["addr"] != "?") then
    event_src.ip = $kv_msg["addr"]
endif

id = "PT_UNIX_like_auditd_syslog_structured_grp_mgmt"

